/* 
Project:				Skills Documentation of ABC/CARE
Author:					Ruby Zhang (rzhang15@uchicago.edu)
Original Date:			April 25, 2017

This file:				Table of variables with cognitive, non-cognitive, parenting, home 

Output: 				Table with variables of four categories
*/

clear all
set more off

// parameters
set seed 1
global bootstraps 50
global quantiles 30

// macros
global projects		: env projects
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
global abccare_output 		= "${klmmexico}/klmPFL/abccare"
global scripts    			= "$projects/abccare-cba/scripts/"
global output      			= "$projects/abccare-cba/output/"

// data
cd $abccare_output
use abccare-mediation-extended.dta, clear

cd $scripts/abccare/genderdifferences
//include abccare-old-factors
include abccare-new-factors

// bootstrap
local j = 0
forvalues b1 = 1/$bootstraps {
	forvalues b2 = 1/$bootstraps {
	
	local j = `j' + 1 
	preserve
	
		if `b1' > 1 & `b2' > 1  {
			bsample
		}
		
		// create factors 
		foreach c in $varstofactor { 
			/*
			if "`c'" != "income" {
				qui keep if adraw == 0
			}
			else {
				qui keep if adraw == `b1'
			}
			*/
			
			local numx : word count ${`c'}
			if `numx' > 1 {
				qui factor  ${`c'} 
				qui predict `c'factor_tmp 
			}
			else {
				gen `c'factor_tmp = `c'
			}
			qui sum `c'factor_tmp 
			qui replace `c'factor_tmp = (`c'factor_tmp - r(mean))/r(sd) 
			xtile `c'factor = `c'factor_tmp, nquantiles($quantiles)
			qui drop `c'factor_tmp
			
			/*
			// standardized variables
			foreach v in ${`c'} {
				qui sum `v'
				qui gen `v'_tmp = (`v' - r(mean))/r(sd)
				drop `v'
				xtile `v' = `v'_tmp, nquantiles($quantiles)
				qui drop `v'_tmp
			}
			*/
		}
		
		// calculate means by gender
		foreach c in $categories {
			foreach v in ${`c'} {
				forvalues s = 0/1 {
					qui sum `v'factor if male == `s'
					matrix `v'`s'_`j' = r(mean)
				
					matrix `v'`s' = (nullmat(`v'`s') \ `v'`s'_`j')
					matrix colnames `v'`s' = `v'`s'
				}
			}
		}
	
	restore
	}
}

// bring to data
local mattoappend
local i = 0

forvalues s = 0/1 {
	foreach c in $categories {
		foreach v in ${`c'} {
		
			local i = `i' + 1
		
			if `i' < 2 * `numvars' {
				local mattoappend `mattoappend' `v'`s',
			}
			else {	
				local mattoappend `mattoappend' `v'`s'
			}
		}
	}
}
di "`mattoappend'"
mat allmeans = (`mattoappend')
clear
svmat allmeans, names(col)
qui gen b = _n

foreach c in $categories {
	
	file open tabfile using "${output}/abccare-new-fac-`c'.tex", replace write
	file write tabfile "\begin{longtable}{c c c c c c}" _n
	file write tabfile "\toprule" _n
	file write tabfile "\textbf{Factor} & \textbf{Male} & \textbf{Male S.E.}  & \textbf{Female} & \textbf{Female S.E.} & \textbf{P-value} \\" _n
	file write tabfile "\midrule" _n
	
	foreach v in ${`c'} {
		
		forvalues s = 0/1 {
			// point estimate
			qui sum `v'`s' if b == 1
			qui gen point`v'`s' = r(mean)
			local point`v'`s'= string(r(mean), "%9.3f")
		
			// empirical mean
			qui sum `v'`s' if b > 1
			qui gen m`v'`s' = r(mean)
		
			// standard errors
			qui sum `v'`s' if b > 1
			local se`v'`s' = string(r(sd), "%9.3f")
		}
	
		// male - female
		qui gen gd`v' = `v'1 - `v'0
	
		// point estimate of male - female
		qui gen gdpoint`v' = point`v'1 - point`v'0
	
		// empirical mean of male - female
		qui sum gd`v' if b > 1 & !missing(gd`v'`s')
		qui gen mgd`v' = r(mean)
		
		// demean
		qui gen dgd`v' = gd`v' - mgd`v' if b > 1
		
		// p-values
		//qui gen dlower`v' = (dgd`v' < gdpoint`v') 		if !missing(dgd`v')
		//qui gen dupper`v' = (dgd`v' > gdpoint`v') 		if !missing(dgd`v')
		qui gen dtwo`v'   = (abs(dgd`v') >= abs(gdpoint`v')) 	if !missing(dgd`v')
		qui sum dtwo`v'
		local ptwo`v' = string(r(mean), "%9.3f")
		
		if `ptwo`v'' <= 0.1 {
			file write tabfile "`v' & `point`v'1' & `se`v'1' &  `point`v'0' & `se`v'0' & \textbf{`ptwo`v''} \\" _n
		}
		else {
			file write tabfile "`v' & `point`v'1' & `se`v'1' &  `point`v'0' & `se`v'0' & `ptwo`v'' \\" _n
		}
	}

	// table

	file write tabfile "\bottomrule" _n
	file write tabfile "\end{longtable}" _n
	file write tabfile "% This file generated by: ${abccare-cba}/scripts/abccare/genderdifferences/abccare-overall-tables.do" _n
	file close tabfile
}
