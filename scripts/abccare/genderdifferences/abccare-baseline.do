/* 
Project:		Mediation
Author:			Anna Ziff (aziff@uchicago.edu)
			Jorge Luis Garcia (jorgelgarcia@uchicago.edu)
Original date:		January 7, 2016
This file:		baseline treatment and gender differences for ABC CARE
Output:			saves a temporal file with differences
*/

// options
clear all
set maxvar  32000, permanently
set matsize 11000, permanently
set more off, permanently

// parameters
set seed 1

// macros
global klmshare			: env klmshare
global projects			: env projects

global abccaredata 	= "${klmshare}/Data_Central/Abecedarian/data/ABC-CARE/extensions/cba-iv"
global repo			= "${projects}/abccare-cba"
global scripts		= "${repo}/scripts/abccare/genderdifferences"

global output 	        = "${repo}/output"

# delimit
global baseline 
m_age_base 
m_work0y 
m_iq0y 
f_home0y 
hh_sibs0y 
hrabc_index 
apgar1 
apgar5 
birthweight 
gestage; 
# delimit cr

cd $abccaredata
use append-abccare_iv.dta, clear
drop if R == 0 & RV == 1

lab var m_age_base 		"Mother's age"
lab var m_work0y 		"Mother works"
lab var m_iq0y			"Mother's IQ"
lab var f_home0y 		"Father at home"
lab var hh_sibs0y		"Number of siblings"
lab var hrabc_index 	"HRI score"
lab var apgar1 			"Apgar score, 1 min."
lab var apgar5 			"Apgar score, 5 min."
lab var birthweight 	"Birthweight"
lab var gestage			"Gestational age"

foreach v in $baseline {
	
	local `v'name : var label `v'
		
	qui reg `v' male
	matrix `v'tab = r(table)
	local bm`v' = `v'tab[1,1]
	local pm`v' = `v'tab[4,1]
	local sm`v' = `v'tab[2,1]
	sum `v' if male == 0
	local mm`v' = r(mean)
	
	
			
	qui reg `v' R 
	matrix `v'tab = r(table)
	local bt`v' = `v'tab[1,1]
	local pt`v' = `v'tab[4,1]
	local st`v' = `v'tab[2,1]
	sum `v' if R == 0
	local mt`v' = r(mean)	
	
	
	mat mat`v' = (`pm`v'',`bm`v'',`mm`v'',`pt`v'',`bt`v'',`mt`v'')
	mat colnames mat`v' = pm bm mm pt bt mt
	mat rownames mat`v' = `v'
	
	mat combine = (nullmat(combine) \ mat`v')
}

clear 
svmat combine, names(col)
gen varname = ""
gen m = _n
forvalues i = 1/10 {
	local lab : word `i' of $baseline
	replace varname = "`lab'" if m == `i'
}
order varname m, first

sort pm
gen morder = _n
gen condition = .
order varname m morder condition, first

local min = 1
forvalues i = 1/10 {
	sum pm if morder == `i'
	if r(mean) > 0.1/(10 + 1 - `i') {
		replace condition = 1
	}	
	else {
		replace condition = 0
	}

}

// make table
file open tabfile using "${output}/abccare-baseline.tex", replace write
file write tabfile "\begin{tabular}{l c c c c c c}" _n
file write tabfile "\toprule" _n
file write tabfile "\mc{1}{c}{Variable} & Female & Male & $ p $ -value & Control & Treatment & $ p $ -value \\" _n
file write tabfile "& Mean & Difference & & Mean & Difference & \\" _n
file write tabfile "\midrule" _n

foreach v in $baseline {


	// formatting
	foreach s in bm pm bt pt {
		local `s'`v' : di %20.2fc ``s'`v''
	}
	foreach s in mm mt {
		local `s'`v' : di %20.2fc ``s'`v''
	}
	//foreach s in m t {
	//	if `p`s'`v'' <= 0.001 {
	//		local b`s'`v' `b`s'`v'' $ ^{***} $
	//	}
	//	else if `p`s'`v'' <= 0.05 & `p`s'`v'' > 0.001 {
	//		local b`s'`v' `b`s'`v'' $ ^{**} $ 
	//	}
	//	else if `p`s'`v'' <= 0.1 & `p`s'`v'' > 0.05 {
	//		local b`s'`v' `b`s'`v'' $ ^{*} $
	//	}
	//}
		


	file write tabfile "``v'name' & `mm`v'' & `bm`v'' & `pm`v'' & `mt`v'' & `bt`v'' & `pt`v'' \\" _n
}
	
file write tabfile "\bottomrule" _n
file write tabfile "\end{tabular}" _n
file write tabfile "% This file generated by: ${mediation}/scripts/abccare/treatment-effects/abccare-baseline.do" _n
file close tabfile
