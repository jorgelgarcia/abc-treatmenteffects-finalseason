/*
Project: 	Treatment effects
Date:		April 27, 2017

This file:	Rank sum test by outcome category
*/

clear all
set more off

// parameters
set seed 1
global bootstraps 1
global quantiles 30

// macros
global projects		: env projects
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
global data	   	= "$klmshare/Data_Central/Abecedarian/data/ABC-CARE/extensions/cba-iv"
global scripts    	= "$projects/abccare-cba/scripts/"
global output      	= "$projects/abccare-cba/output/"

local fullcond
local altcond 	& ((dc_mo_pre > 0 & dc_mo_pre != . & R == 0) | R == 1)
local homecond 	& ((dc_mo_pre == 0 & R == 0) | R == 1)

// data
cd $data
use append-abccare_iv, clear

drop if R == 0 & RV == 1

cd ${scripts}/abccare/genderdifferences
include abccare-112-outcomes


foreach c in `categories' {
	foreach v in ``c'' {
		if substr("`v'",1,6) == "factor" {
			gen `v' = .
		}
		
		forvalues s = 0/1 {
			local tofactor
			if substr("`v'",1,6) == "factor" {
				foreach v2 in ``v'' {
					sum `v2'
					gen std`v2'`s' = (`v2' - r(mean))/r(sd)
					local tofactor `tofactor' std`v2'`s'
				}
				cap factor `tofactor'
				if !_rc {
					cap predict `v'`s'
					if _rc {
						gen `v'`s' = .
					}
				}
				replace `v' = `v'`s' if male == `s'
			}
			
			foreach samp in full alt home {
			
				reg `v' R if male == `s' ``samp'cond'
				matrix b`v'`s'`samp' = e(b)
				matrix `c'`s'`samp' = (nullmat(`c'`s'`samp') \ b`v'`s'`samp'[1,1])
				matrix colnames `c'`s'`samp' = `c'`s'`samp'
			}
		}
	}
}


// rank sign test by category
file open tabfile using "${output}/abccare-gdiff-category-ranksum.tex", replace write
file write tabfile "\begin{tabular}{l c c c c}" _n
file write tabfile "\toprule" _n
file write tabfile "\mc{1}{c}{Category} & \# Outcomes & Full Control & Stay at Home & Alternative Preschool \\" _n
file write tabfile "\midrule" _n	
		

foreach c in `categories' {
	foreach samp in full alt home {
		clear
		mat all`c'`samp' = `c'0`samp', `c'1`samp'
		svmat all`c'`samp'
	
		signrank all`c'`samp'1 = all`c'`samp'2
		local z`c'`samp' = r(z)
		local p`c'`samp' = 2* normprob(-abs(`z`c'`samp''))
		local p`c'`samp' = string(`p`c'`samp'', "%9.3f")
		di "`p`c'`samp''"
	}
	local `c'_N : word count ``c''
	
	
	file write tabfile "``c'_name' & ``c'_N' & `p`c'full' & `p`c'home' & `p`c'alt' \\" _n
}


file write tabfile "\bottomrule" _n
file write tabfile "\end{tabular}" _n
file write tabfile "% This file generated by: abccare-cba/scripts/abccare/genderdifferences/abccare-gdiff-category-ranksum.do" _n
file close tabfile
