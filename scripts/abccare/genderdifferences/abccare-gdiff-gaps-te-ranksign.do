/*
Project: 	Treatment effects
Date:		April 27, 2017

This file:	Means of control group
*/

clear all
set more off

// parameters
set seed 1
global bootstraps 1000
global quantiles 30

// macros
global projects		: env projects
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
global data	   	= "$klmshare/Data_Central/Abecedarian/data/ABC-CARE/extensions/cba-iv"
global scripts    	= "$projects/abccare-cba/scripts/"
global output      	= "$projects/abccare-cba/output/"

// data
cd $data
use append-abccare_iv, clear

drop if R == 0 & RV == 1

// variables
cd ${scripts}/abccare/genderdifferences
include abccare-reverse
include abccare-112-outcomes

foreach c in `categories' {
	if "`c'" != "all" {
		foreach v in ``c'' {
	
		
			if substr("`v'",1,6) == "factor" {
				gen `v' = .
			}
		
			forvalues s = 0/1 {
				local tofactor
				if substr("`v'",1,6) == "factor" {
					foreach v2 in ``v'' {
						sum `v2'
					gen std`v2'`s' = (`v2' - r(mean))/r(sd)
						local tofactor `tofactor' std`v2'`s'
					}
					cap factor `tofactor'
					if !_rc {
						cap predict `v'`s'
						if _rc {
							gen `v'`s' = .
						}
					}
					replace `v' = `v'`s' if male == `s'
				}
			}
		}
	}
}

forvalues b = 0/$bootstraps {
	di "`b'"
	preserve
	
	if `b' > 0 {
		bsample
	}
	
	foreach c in `categories' {
		local counter0 = 0			// females
		local counter1 = 0			// males
		local numvars : word count ``c'' 	// number of variables
	
		foreach v in ``c'' {
			
			forvalues s = 0/1 {
				qui sum `v' if male == `s' & R == 0  //& dc_mo_pre > 0 & dc_mo_pre != . //& dc_mo_pre == 0
				local `v'`s'`b'_R0 = r(mean)
				qui sum `v' if male == `s' & R == 1
				local `v'`s'`b'_R1 = r(mean)
				
				if ``v'`s'`b'_R1' > ``v'`s'`b'_R0' {
					local counter`s' = `counter`s'' + 1
				}
				
			}
		}
		forvalues s = 0/1 {
			matrix `c'_prop`s'`b' = `counter`s'' / `numvars'
			matrix `c'_prop`s' = (nullmat(`c'_prop`s') \ `c'_prop`s'`b')
			matrix colnames `c'_prop`s' = `c'`s'
		}
	}
	
	restore
}

// bring to data
local n = 0
local numcats : word count `categories'

foreach c in `categories' {
	forvalues s = 0/1 {
		local n = `n' + 1
		if `n' < 2 * `numcats'  {
			local formatrix `formatrix' `c'_prop`s', 
		}
		else {
			local formatrix `formatrix' `c'_prop`s'
		}
	}
}

matrix all = `formatrix'


clear
svmat all, names(col)
gen draw = _n


// inference
foreach c in `categories' {
	signrank `c'1 = `c'0
	local p`c' = 2 * normprob(-abs(r(z)))
	if `p`c'' <= 0.101 {
		local sig = 1
	}
	else {
		local sig = 0
	}
	
	local p`c' = string(`p`c'', "9.3f")

	qui gen `c'_0_1 = `c'1 - `c'0
	sum `c'_0_1 if draw == 1
	local `c'_0_1 = string(r(mean), "%9.3f")
	
	if `sig' == 1 {
		local `c'_0_1 "\textbf{``c'_0_1'}"
	}
	
	// test if =50
	forvalues s = 0/1 {
		sum `c'`s' if draw == 1
		gen point`c'`s' = r(mean)
		
		sum `c'`s' if draw > 1
		gen emp`c'`s' = r(mean)
		
		gen dm`c'`s' = `c'`s' - emp`c'`s' + 0.5
		
		gen diff1`c'`s' = (dm`c'`s' < point`c'`s') if draw > 1
		gen diff2`c'`s' = (dm`c'`s' > point`c'`s') if draw > 1
		sum diff1`c'`s'
		local p1`c'`s' = r(mean)
		sum diff2`c'`s'
		local p2`c'`s' = r(mean)
	
		sum `c'`s' if draw == 1
		local `c'`s' = r(mean)
		if `p1`c'`s'' <= 0.101 | `p2`c'`s'' <= 0.101 {
			local sig50 = 1
		}
		else {
			local sig50 = 0
		}
		local `c'`s' = string(``c'`s'', "%9.3f")
		if `sig50' == 1 {
			local `c'`s' "\textbf{``c'`s''}"
		}
	}
	
}

file open tabfile using "${output}/abccare-proportion-across-exp-full.tex", replace write
file write tabfile "\begin{tabular}{l c c c c}" _n
file write tabfile "\toprule" _n
file write tabfile "Category & \# Outcomes & \mc{2}{c}{Proportion} & Difference \\" _n
file write tabfile "\cmidrule(lr){3-4} \cmidrule(lr){5-5}" _n
file write tabfile "		&			& Male & Female & Male $ - $ Female \\" _n
file write tabfile "\midrule" _n	

foreach c in `categories' {
	local `c'_N : word count ``c''
	
	if "`c'" == "all" {
		file write tabfile "\midrule" _n
	}
	file write tabfile "``c'_name' & ``c'_N' & ``c'1' & ``c'0' & ``c'_0_1' \\" _n
}

file write tabfile "\bottomrule" _n
file write tabfile "\end{tabular}" _n
file write tabfile "% This file generated by: abccare-cba/scripts/abccare/genderdifferences/abccare-gdiff-gaps-ranksign.do" _n
file close tabfile
