/* 
Project:				Skills Documentation of ABC/CARE
Author:					Ruby Zhang (rzhang15@uchicago.edu)
Original Date:			April 25, 2017

This file:				Table of variables with cognitive, non-cognitive, parenting, home 

Output: 				Table with variables of four categories
*/

clear all
set more off

// parameters
set seed 1
global bootstraps 200
global quantiles 30

// macros
//global projects		: env projects
global projects = "~/Desktop/CEHD/Projects"
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
//global abccare_output 		= "${klmmexico}/klmPFL/abccare"
global abccare_output = "$projects/ABCCARE/"
global scripts    			= "$projects/abccare-cba/scripts/"
global output      			= "$projects/abccare-cba/output/"

// data
cd $abccare_output
use abccare-mediation-extended.dta, clear

// looking only at control group
drop if R==1

// variables
cd $scripts/abccare/genderdifferences
include abccare-label

//include abccare-self-variables
include abccare-outcome-variables

// Constructing the table

foreach c in $categories {

	file open tabfile using "${output}/abccare-outcome-var-`c'.tex", replace write
	file write tabfile "\begin{longtable}{c c c c c c}" _n
	file write tabfile "\toprule" _n
	file write tabfile "\textbf{Variable} & \textbf{Male} & \textbf{Male S.E.}  & \textbf{Female} & \textbf{Female S.E.} & \textbf{$ p $-value} \\" _n
	file write tabfile "\midrule" _n
	
	foreach var_list in ${`c'} {
		//Bootstrapping for mean
		forvalues b = 0/$bootstraps {

			preserve
			
				if `b' > 0 {
					bsample			// this resamples with replacement
				}					// don't do this at `b'==0 to get point estimate 
									// (with original sample)
					
				sum `var_list' if male==0
				local mean_f = r(mean)
				sum `var_list' if male==1
				local mean_m = r(mean)
				matrix mean`b' = `mean_f' - `mean_m'
				
				// two matrices
				matrix allmeans = (nullmat(allmeans) \ mean`b')
			
			restore
		}

		preserve 
			clear
			svmat allmeans
	
			// hypothesis testing
			gen n = _n
			sum allmeans1 if n == 1
			gen point = r(mean)
	
			sum allmeans1 if n > 1
			gen empirical_mean = r(mean)
		
			// test if mean for control group same as treatment group
			gen demean = allmeans1 - empirical_mean if n > 1
			gen diffxgen = (abs(demean) >= abs(point)) if n > 1
			sum diffxgen
			local p_sex = string(r(mean), "%9.3f") 
		restore
		
		preserve
			collapse (mean) mean`var_list' = `var_list' (sem) se`var_list' =`var_list', by(male)
			
			local mean`var_list'0 = string(mean`var_list'[1], "%9.3f")
			local mean`var_list'1 = string(mean`var_list'[2], "%9.3f")
			
			local se`var_list'0 = string(se`var_list'[1], "%9.3f")
			local se`var_list'1 = string(se`var_list'[2], "%9.3f")
		restore
		
		if `p_sex' <= 0.1 {
			file write tabfile "`name_`var_list'' & \textbf{`mean`var_list'1'} & `se`var_list'1' &  \textbf{`mean`var_list'0'} & `se`var_list'0' & `p_sex' \\" _n
		}
		else {
			file write tabfile "`name_`var_list'' & `mean`var_list'1' & `se`var_list'1' &  `mean`var_list'0' & `se`var_list'0' & `p_sex' \\" _n
		}
		mat drop allmeans

	}
		
	file write tabfile "\bottomrule" _n
	file write tabfile "\end{longtable}" _n
	file write tabfile "% This file generated by: ${abccare-cba}/scripts/abccare/genderdifferences/abccare-tables.do" _n
	file close tabfile
}
