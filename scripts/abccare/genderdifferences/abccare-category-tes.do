/*
Project: 	Treatment effects
Date:		April 27, 2017

This file:	Means of control group
*/

clear all
set maxvar 30000
set matsize 11000
set more off

// parameters
set seed 1
global bootstraps 25
global maxtries 20
global quantiles 30

// macros
global projects		: env projects
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
global data	   	= "$klmshare/Data_Central/Abecedarian/data/ABC-CARE/extensions/cba-iv"
global scripts    	= "$projects/abccare-cba/scripts/"
global output      	= "$projects/abccare-cba/output/"

// data
cd $data
use append-abccare_iv, clear

drop if R == 0 & RV == 1

// variables
cd ${scripts}/abccare/genderdifferences
qui {
	include abccare-reverse
	include abccare-112-outcomes


foreach c in `categories' {
	if "`c'" != "all" {
		foreach v in ``c'' {
	
		
			if substr("`v'",1,6) == "factor" {
				gen `v' = .
			}
		
			forvalues s = 0/1 {
				local tofactor
				if substr("`v'",1,6) == "factor" {
					foreach v2 in ``v'' {
						sum `v2'
					gen std`v2'`s' = (`v2' - r(mean))/r(sd)
						local tofactor `tofactor' std`v2'`s'
					}
					cap factor `tofactor'
					if !_rc {
						cap predict `v'`s'
						if _rc {
							gen `v'`s' = .
						}
					}
					replace `v' = `v'`s' if male == `s'
				}
			}
		}
	}
}

}

cap log close
log using health, replace


forvalues s = 0/1 {

	local noall
	local all

	foreach c in `categories' {
	
		local b = 0
		global success`s'_`c' = 0
		local tries = 0
		
		if "`c'" == "all" {
			foreach l in `categories' {
				if "`l'" != "all" {
					local testvar = 0
					foreach m in `noall' {
					
						if "`l'" == "`m'" {
							local testvar = 1
						}
					}
					if `testvar' == 0 {
						local all `all' ``l''
					
					}
				
				}
			
			}
		}
	
		while (${success`s'_`c'} <= $bootstraps) & (`tries' <= $maxtries) {
	
			preserve
	
			if `b' > 0 {
				bsample
			}
	
			di "sex`s' `c' `b'"
	
			// average treatment effect
			
			foreach v in ``c'' {
	
				qui sum `v' 
				qui replace `v' = (`v' - r(mean) ) / r(sd)
		
				qui sum `v' if male == `s' & R == 0 //& dc_mo_pre > 0 & dc_mo_pre != . // & dc_mo_pre == 0 //
				local b`v'`s'`b'_R0 = r(mean)
					
				qui sum `v' if male == `s' & R == 1
				local b`v'`s'`b'_R1 = r(mean)
				
				// calculate treatment effect and store by category
				matrix te`s'_`c'`b' = (nullmat(te`s'_`c'`b') \ `b`v'`s'`b'_R1' - `b`v'`s'`b'_R0')
				
				if (`b`v'`s'`b'_R1' - `b`v'`s'`b'_R0') == . { // keep track if mising
					qui sum `v' if male == `s' & R == 0
					qui sum `v' if male == `s' & R == 1
						
					matrix fail`s'`c' = (nullmat(fail`s'`c') \ `b')
					global fail`s'`c' ${fail`s'`c'} "`v'"
						
				}
			}
		
			mat ones`s'_`c'`b' = J(rowsof(te`s'_`c'`b'),1,1)
			mat sum`s'_`c'`b' = ones`s'_`c'`b''*te`s'_`c'`b'
			mat avg`s'_`c'`b' = sum`s'_`c'`b'/rowsof(te`s'_`c'`b')	
			
			if avg`s'_`c'`b'[1,1] != . {
				mat `c'`s' = (nullmat(`c'`s') \ avg`s'_`c'`b')
				mat colnames `c'`s' = `c'`s'
			
				global success`s'_`c' = ${success`s'_`c'} + 1
				
				local b = `b' + 1
				local tries = 0
			}
			
			else {
				mat drop te`s'_`c'`b' 
				local tries = `tries' + 1
				if `tries' == $maxtries {
					local noall `noall' `c'
				}
			}
		
		restore	
		}
	}
}

log close 

// inference
global finalcat
foreach c in `categories' {
	forvalues s = 0/1 {
		cap confirm mat `c'`s'
		if !_rc {
			mat combined = (nullmat(combined) , `c'`s')
			global finalcat $finalcat `c'`s'
		}
	}
}

clear
svmat combined, names(col)
gen b = _n

foreach c in $finalcat {
	
	//forvalues s = 0/1 {
	
		// empirical mean
		qui sum `c' if b > 1
		qui gen `c'_em = r(mean)
		local `c'_em = r(mean)
		local `c'_em : di %9.3fc ``c'_em'
		
		// point estimate
		qui sum `c' if b == 1
		qui gen `c'_pe = r(mean)
		local `c'_pe = r(mean) 
		local `c'_pe : di %9.3fc ``c'_pe'
		
		// demean
		qui gen `c'_dm = `c' - `c'_em if b > 1
		
		// pvalue
		qui gen `c'_di = (`c'_dm >= `c'_em) if b > 1
		qui sum `c'_di
		qui gen `c'_p = r(mean)
		
		if `c'_p <= 0.1 {
			local `c'_em "\bm{``c'_em'}"
		}
		
	//}
}


// write table
file open tabfile using "${output}/abccare-category-tes-test.tex", replace write
file write tabfile "\begin{tabular}{l c c c}" _n
file write tabfile "\toprule" _n
file write tabfile "Category & \# Outcomes & \mc{2}{c}{Mean Treatment Effect}  \\" _n
file write tabfile "\cmidrule(lr){3-4}" _n
file write tabfile "		&	&  Female & Male  \\" _n
file write tabfile "\midrule" _n	

foreach c in `categories' {
	local `c'_N : word count ``c''
	
	if "`c'" == "all" {
		file write tabfile "\midrule" _n
	}
	file write tabfile "``c'_name' & $ ``c'_N' $ & $ ``c'0_em' $ & $ ``c'1_em' $ \\" _n
}

file write tabfile "\bottomrule" _n
file write tabfile "\end{tabular}" _n
file write tabfile "% This file generated by: abccare-cba/scripts/abccare/genderdifferences/abccare-category-tes.do" _n
file close tabfile
