/*
Project: 	Treatment effects
Date:		September 13, 2017

This file:	abccare-gdiff-gaps-ranksign FOR PERRY
*/

clear all
set more off
set maxvar 30000
set matsize 11000

// parameters
set seed 1
global bootstraps 	10
global quantiles 	30

// macros
global projects		: env projects
global klmshare		: env klmshare
global klmmexico	: env klmMexico

// filepaths
global data50 		= "$klmshare/ziff/p_temp"
global data	   		= "$klmshare/Data_Central/data-repos-old/perry/base"
global scripts    	= "$projects/abccare-cba/scripts/"
global output      	= "$projects/abccare-cba/output/"

// data
cd $data
use perry-base, clear

cd $data50
merge 1:1 id using p50cogncog, nogen
merge 1:1 id using p50crime, nogen
merge 1:1 id using p50employment, nogen
merge 1:1 id using p50health, nogen

// variables
cd ${scripts}/abccare/genderdifferences
qui include "perry50-112-outcomes"

qui {
foreach c in `categories' {
	if "`c'" != "all" {
		foreach v in ``c'' {
	
		
			if substr("`v'",1,6) == "factor" {
				gen `v' = .
			}
		
			forvalues s = 0/1 {
				local tofactor
				if substr("`v'",1,6) == "factor" {
					foreach v2 in ``v'' {
						sum `v2'
					gen std`v2'`s' = (`v2' - r(mean))/r(sd)
						local tofactor `tofactor' std`v2'`s'
					}
					cap factor `tofactor'
					if !_rc {
						cap predict `v'`s'
						if _rc {
							gen `v'`s' = .
						}
					}
					replace `v' = `v'`s' if male == `s'
				}
			}
		}
	}
}

}

forvalues b = 0/$bootstraps {
	
	preserve
	
	if `b' > 0 {
		bsample
	}
	
	di "`b'"
	
	
	foreach c in `categories' {
		
		// average treatment effect
		foreach v in ``c'' {
		
			if "`c'" != "all" {
				qui sum `v' 
				qui replace `v' = (`v' - r(mean) ) / r(sd)
		
		
				forvalues s = 0/1 {
		
					sum `v' if male == `s' & treatment == 0  //& dc_mo_pre == 0 //dc_mo_pre > 0 & dc_mo_pre != . //
					local b`v'`s'`b'_R0 = r(mean)
					sum `v' if male == `s' & treatment == 1
					local b`v'`s'`b'_R1 = r(mean)
				
					// calculate treatment effect and store by category
					matrix te`s'_`c'`b' = (nullmat(te`s'_`c'`b') \ `b`v'1`b'_R1' - `b`v'0`b'_R0')
					matrix te`s'_all`b' = (nullmat(te`s'_all`b') \ `b`v'1`b'_R1' - `b`v'0`b'_R0')
				}
			}
		}
		
		forvalues s = 0/1 {
			mat ones`s'_`c'`b' = J(rowsof(te`s'_`c'`b'),1,1)
			mat sum`s'_`c'`b' = ones`s'_`c'`b''*te`s'_`c'`b'
			mat avg`s'_`c'`b' = sum`s'_`c'`b'/rowsof(te`s'_`c'`b')
			mat list avg`s'_`c'`b'
			
			if avg`s'_`c'`b'[1,1] != . & `b' > 0 {
				mat `c'`s' = (nullmat(`c'`s') \ avg`s'_`c'`b')
				mat colnames `c'`s' = `c'`s'
			}
			else {
				mat `c'`s'_prob = (nullmat(`c'`s'_prob) \ `b')
			}
			
		}
	}
	
	restore
}

// inference
foreach c in `categories' {
	forvalues s = 0/1 {
		if rowsof(`c'`s') < $bootstraps + 1 {
			local n = $bootstraps + 1 - rowsof(`c'`s')
			mat add = J(`n', 1,.)
			mat `c'`s' = (`c'`s' \ add)
		}
	}

	mat combined = (nullmat(combined) , `c'0, `c'1)
}

clear
svmat combined, names(col)
gen b = _n

foreach c in `categories' {
	
	forvalues s = 0/1 {
	
		// empirical mean
		qui sum `c'`s' if b > 1
		qui gen `c'`s'_em = r(mean)
		
		// point estimate
		qui sum `c'`s' if b == 1
		qui gen `c'`s'_pe = r(mean)
		local `c'`s'_pe = r(mean) 
		local `c'`s'_pe : di %9.3fc ``c'`s'_pe'
		
		// demean
		qui gen `c'`s'_dm = `c'`s' - `c'`s'_em if b > 1
		
		// pvalue
		qui gen `c'`s'_di = (`c'`s'_dm >= `c'`s'_pe) if b > 1
		qui sum `c'`s'_di
		qui gen `c'`s'_p = r(mean)
		
		if `c'`s'_p <= 0.1 {
			local `c'`s'_pe "\bm{``c'`s'_pe'}"
		}
		
	}
}


// write table
file open tabfile using "${output}/perry-category-tes.tex", replace write
file write tabfile "\begin{tabular}{l c c c}" _n
file write tabfile "\toprule" _n
file write tabfile "Category & \# Outcomes & \mc{2}{c}{Mean Treatment Effect}  \\" _n
file write tabfile "\cmidrule(lr){3-4}" _n
file write tabfile "		&	&  Female & Male  \\" _n
file write tabfile "\midrule" _n	

foreach c in `categories' {
	local `c'_N : word count ``c''
	
	if "`c'" == "all" {
		file write tabfile "\midrule" _n
	}
	file write tabfile "``c'_name' & $ ``c'_N' $ & $ ``c'0_pe' $ & $ ``c'1_pe' $ \\" _n
}

file write tabfile "\bottomrule" _n
file write tabfile "\end{tabular}" _n
file write tabfile "% This file generated by: abccare-cba/scripts/abccare/genderdifferences/perry-category-tes.do" _n
file close tabfile
